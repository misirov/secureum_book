\section{Security Pitfalls \& Best Practices 101 Quiz Solutions}

\textbf{1. The use of \texttt{pragma} in the given contract snippet}\label{sec:exam4_q1}

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity ^0.6.0;

contract test {
   // Assume other required functionality is correctly implemented
   // Assume this contract can work correctly without modifications across 0.6.x/0.7.x/0.8.x compiler versions
}
\end{lstlisting}

\textbf{Solution: B \& C.}

Contracts should be deployed using the same compiler version/flags with which they have been tested.
Locking the pragma (for e.g. by not using `\verb|^|' in \verb|pragma solidity 0.5.10|) ensures that contracts do not accidentally get deployed using an older compiler version with unfixed bugs.\\

There were bugs that were fixed in versions between \verb|0.6.5| and \verb|0.6.11| which means those fixes were absent in \verb|0.6.5|.
Choice B was about these aspects.\\

Illustrative means ``serving as an example or explanation''.
So the use of \verb|^0.6.0| when the latest available version is \verb|0.8.9| (as mentioned in choice C) is an example of using a much older compiler version when newer versions with bug fixes and more security features e.g. built-in overflow checks in \verb|^0.8.0| are available.\\

\textbf{2. The given contract snippet has}\label{sec:exam4_q2}

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {

   // Assume other required functionality is correctly implemented

   function kill() public {
      selfdestruct(payable(0x0));
   }
}
\end{lstlisting}

\textbf{Solution: A \& B.}

Unprotected call to \verb|selfdestruct|: A user/attacker can mistakenly/intentionally kill the contract. Protect access to such functions.\\


Zero Address Check: \verb|address(0)| which is 20-bytes of 0's is treated specially in Solidity contracts because the private key corresponding to this address is unknown.
Ether and tokens sent to this address cannot be retrieved and setting access control roles to this address also won't work (no private key to sign transactions).
Therefore zero addresses should be used with care and checks should be implemented for user-supplied address parameters.\\

Reserved Keywords: These keywords are reserved in \verb|Solidity|. They might become part of the syntax in the future: \verb|after|, \verb|alias|, \verb|apply|, \verb|auto|, \verb|case|, \verb|copyof|, \verb|default|, \verb|define|, \verb|final|, \verb|immutable|, \verb|implements|, \verb|in|, \verb|inline|, \verb|let|, \verb|macro|, \verb|match|, \verb|mutable|, \verb|null|, \verb|of|, \verb|partial|, \verb|promise|, \verb|reference|, \verb|relocatable|, \verb|sealed|, \verb|sizeof|, \verb|static|, \verb|supports|, \verb|switch|, \verb|typedef|, \verb|typeof|, \verb|unchecked|.\\

\textbf{3. The given contract snippet has}\label{sec:exam4_q3}

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {

    // Assume other required functionality is correctly implemented

    modifier onlyAdmin() {
        // Assume this is correctly implemented
        _;
    }

    function transferFunds(address payable recipient, uint amount) public {
        recipient.transfer(amount);
   }
}
\end{lstlisting}

\textbf{Solution: A \& C.}

Contract functions executing critical logic should have appropriate access control enforced via address checks (e.g. \verb|owner|, \verb|controller|, etc\dots) typically in modifiers.
Missing checks allow attackers to control critical logic.\\

Unprotected (\verb|external|/\verb|public|) function calls sending Ether/tokens to user-controlled addresses may allow users to withdraw unauthorized funds.\\

\verb|transferFunds()| clearly lets anyone withdraw any amount to any address.
The only hint in the question is the \verb|onlyAdmin| modifier.
While some other access control may also have been acceptable, the focus is on the code snippet provided and hence A is true.\\

\verb|transfer| (unlike \verb|send|) does not return a success/failure return value.
It reverts on failure.
So there is nothing to be checked.
Note that \verb|ERC20|'s \verb|transfer()| returns a boolean which should be checked.\\

\pagebreak

\textbf{4. In the given contract snippet}\label{sec:exam4_q4}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {

    // Assume other required functionality is correctly implemented

    mapping (uint256 => address) addresses;
    bool check;

    modifier onlyIf() {
        if (check) {
            _;
        }
    }

    function setAddress(uint id, address addr) public {
        addresses[id] = addr;
    }

    function getAddress(uint id) public onlyIf returns (address) {
        return addresses[id];
    }
}
\end{lstlisting}

\textbf{Solution: A, \& B.}

If a modifier does not execute `\verb|_|' or reverts, the function using that modifier will return the default value causing unexpected behavior.\\


Remember that function modifiers can be used to change the behavior of functions in a declarative way.
For example, you can use a modifier to automatically check a condition prior to executing the function.
The function's control flow continues after the `\verb|_|'' in the preceding modifier.
Multiple modifiers are applied to a function by specifying them in a whitespace-separated list and are evaluated in the order presented.
The modifier can choose not to execute the function body at all and in that case the return variables are set to their default values just as if the function had an empty body.
The `\verb|_|' symbol can appear in the modifier multiple times.
Each occurrence is replaced with the function body.\\

\pagebreak

\textbf{5. The security concern(s) in the given contract snippet is/are}\label{sec:exam4_q5}

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {

    // Assume other required functionality is correctly implemented

    modifier onlyAdmin {
     // Assume this is correctly implemented
        _;
    }

    function delegate (address addr) external {  addr.delegatecall(abi.encodeWithSignature("setDelay(uint256)"));
    }
}
\end{lstlisting}

\textbf{Solution: A, B, C \& D.}

\verb|delegatecall()| or \verb|callcode()| to an address controlled by the user allows execution of malicious contracts in the context of the caller's state.
Ensure trusted destination addresses for such calls.\\

Ensure that return values of low-level calls (\verb|call|/\verb|callcode|/\verb|delegatecall|\linebreak /\verb|send|/etc\dots) are checked to avoid unexpected failures.\\

Contract functions executing critical logic should have appropriate access control enforced via address checks (e.g. \verb|owner|, \verb|controller|, etc\dots) typically in modifiers.
Missing checks allow attackers to control critical logic.\\

Low-level calls \verb|call|/\verb|delegatecall|/\verb|staticcall| return true even if the account called is non-existent (per EVM design).
Account existence must be checked prior to calling if needed.\\

\pagebreak

\textbf{6. The vulnerability/vulnerabilities present in the given contract snippet is/are}\label{sec:exam4_q6}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.7.0;
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
// which works with 0.7.0

contract test {

 // Assume other required functionality is correctly implemented
 // For e.g. users have deposited balances in the contract
 // Assume nonReentrant modifier is always applied

    mapping (address => uint256) balances;

    function withdraw(uint256 amount) external nonReentrant {
        msg.sender.call{value: amount}("");
        balances[msg.sender] -= amount;
    }
}
\end{lstlisting}

\textbf{Solution: B \& C.}

The code in this question was unintentionally missing inheritance from the \verb|ReentrancyGuard| Contract.
While there's a lot of discussion about the correct meaning of the term ``underflow'', this is how it is used in the \verb|Solidity| Documentation and other related literature.\\

Untrusted external contract calls could callback leading to unexpected results such as multiple withdrawals or out-of-order events.
Use check-effects-\linebreak interactions pattern or reentrancy guards.\\

Not using \verb|OpenZeppelin|'s \verb|SafeMath| (or similar libraries) that check for overflows/underflows may lead to vulnerabilities or unexpected behavior if user/attacker can control the integer operands of such arithmetic operations.\linebreak
\verb|Solc v0.8.0| introduced default overflow/underflow checks for all arithmetic operations.\\

I hope that the comment

\begin{lstlisting}[language=Solidity, style=solStyle]
// Assume nonReentrant modifier is always applied|
\end{lstlisting}

implied that the intent was to apply the modifier ``correctly'' (i.e. with successful compilation), which further implies reentrancy risk mitigation i.e. A is not a correct choice.
Also, if A were to also be correct then that would again result in the ``All of the above'' ambiguity (A + B + C or D or A + B + C + D).\\

\pagebreak

\textbf{7. The security concern(s) in the given contract snippet is/are}\label{sec:exam4_q7}

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {

 // Assume other required functionality is correctly implemented

    uint256 private constant secret = 123;

    function diceRoll() external view returns (uint256) {
        return (((block.timestamp * secret) % 6) + 1);
    }
}
\end{lstlisting}

\textbf{Solution: B \& C.}

Marking variables \verb|private| does not mean that they cannot be read on-chain.
Private data should not be stored unencrypted in contract code or state but instead stored encrypted or off-chain.\\

PRNG relying on \verb|block.timestamp|, \verb|now| or \verb|blockhash| can be influenced by miners to some extent and should be avoided.\\

Making it public in this case should not affect gas given that there are no function arguments to copy over (if there were parameters/arguments, making it public would increase gas).
Even otherwise, making it public from external should not affect the attack surface of the contract because it will only further allow (trusted) contract functions to call it.\\

In addition, the logic of \verb|diceRoll()| is broken as it returns only 1 or 4.\\

\pagebreak

\textbf{8. The security concern(s) in the given contract snippet is/are}\label{sec:exam4_q8}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {

    // Assume other required functionality is correctly implemented
    // Contract admin set to deployer in constructor (not shown)
    address admin;

    modifier onlyAdmin {
        require(tx.origin == admin);
        _;
    }

    function emergencyWithdraw() external payable onlyAdmin {
        msg.sender.transfer(address(this).balance);
    }
}
\end{lstlisting}

\textbf{Solution: B \& D.}

Although \verb|transfer()| and \verb|send()| have been recommended as a security best-practice to prevent reentrancy attacks because they only forward 2300 gas, the gas repricing of opcodes may break deployed contracts.
Use \verb|call()| instead, without hardcoded gas limits along with checks-effects-interactions pattern or reentrancy guards for reentrancy protection.\\

Use of \verb|tx.origin| for authorization may be abused by a MITM malicious contract forwarding calls from the legitimate user who interacts with it.
Use \verb|msg.sender| instead.\\

Regarding C, 0 transfers should not revert.
Even if they did, in this context, it wouldn't be considered a ``security'' concern because there would be nothing to withdraw and so a revert wouldn't be a concern w.r.t. any locked funds as such.\\

Events for critical state changes (e.g. owner and other critical parameters) should be emitted for tracking this off-chain.\\

\pagebreak

\textbf{9. The given contract snippet is vulnerable because of}\label{sec:exam4_q9}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {

    // Assume other required functionality is correctly implemented

    uint256 private constant MAX_FUND_RAISE = 100 ether;
    mapping (address => uint256) contributions;

    function contribute() external payable {
        require(address(this).balance != MAX_FUND_RAISE);
        contributions[msg.sender] += msg.value;
    }
}
\end{lstlisting}

\textbf{Solution: D.}

\verb|external| or \verb|public| is required for a function to be \verb|payable|.
This use of \verb|msg.sender| is very common and correct.\\

Use of strict equalities with tokens/Ether can accidentally/maliciously cause unexpected behavior.
Consider using \verb|>=| or \verb|<=| instead of \verb|==| for such variables depending on the contract logic.\\

A contract can receive Ether via \verb|payable| functions, \verb|selfdestruct()|, \verb|coinbase| transaction or pre-sent before creation.
Contract logic depending on \verb|this.balance| can therefore be manipulated.\\

Given the compiler version, even if there is an attempted integer overflow at runtime, it will revert before overflowing (because of inbuilt checks) with an exception but will not wrap.
So A is not a vulnerability.
While this is true in general, this snippet cannot be overflowed because of its dependence on \verb|msg.value|.\\

\pagebreak

\textbf{10. In the given contract snippet, the require check will}\label{sec:exam4_q10}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {

    // Assume other required functionality is correctly implemented

    function callMe (address target) external {
        (bool success, ) = target.call("");
        require(success);
    }
}
\end{lstlisting}

\textbf{Solution: B.}

Low-level calls \verb|call|/\verb|delegatecall|/\verb|staticcall| return \verb|true| even if the account called is non-existent (per EVM design).
Account existence must be checked prior to calling if needed.\\

\textbf{11. The security concern(s) in the given contract snippet is/are}\label{sec:exam4_q11}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {

    // Assume other required functionality is correctly implemented
    // Assume admin is set correctly to contract deployer in constructor
    address admin;

    function setAdmin (address _newAdmin) external {
        admin = _newAdmin;
    }
}
\end{lstlisting}

\textbf{Solution: A, B, C \& D.}

Contract functions executing critical logic should have appropriate access control enforced via address checks (e.g. owner, controller etc.) typically in modifiers.
Missing checks allow attackers to control critical logic.\\

Setters of address type parameters should include a zero-address check otherwise contract functionality may become inaccessible or tokens burnt forever.\\

Changing critical addresses in contracts should be a two-step process where the first transaction (from the old/current address) registers the new address (i.e. grants ownership) and the second transaction (from the new address) replaces the old address with the new one (i.e. claims ownership).
This gives an opportunity to recover from incorrect addresses mistakenly used in the first step.
If not, contract functionality might become inaccessible.\\

Events for critical state changes (e.g. owner and other critical parameters) should be emitted for tracking this off-chain.\\

\textbf{12. The security concern(s) in the given contract snippet is/are}\label{sec:exam4_q12}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {

    // Assume other required functionality is correctly implemented

    address admin;
    address payable pool;

   constructor(address _admin) {
        admin = _admin;
    }

    modifier onlyAdmin {
        require(msg.sender == admin);
        _;
    }

    function setPoolAddress(address payable _pool) external onlyAdmin {
        pool = _pool;
    }

    function addLiquidity() payable external {
        pool.transfer(msg.value);
    }
}
\end{lstlisting}

\textbf{Solution: A, C \& D.}

Externally accessible functions (\verb|external|/\verb|public| visibility) may be called in any order (with respect to other defined functions).
It is not safe to assume they will only be called in the specific order that makes sense to the system design or is implicitly assumed in the code.
For e.g., initialization functions (used with upgradeable contracts that cannot use constructors) are meant to be called before other system functions can be called.\\

Uninitialized state/local variables are assigned zero values by the compiler and may cause unintended results e.g. transferring tokens to zero address.
Explicitly initialize all state/local variables.\\

Setters of \verb|address| type parameters should include a zero-address check otherwise contract functionality may become inaccessible or tokens burnt forever.\\

Race conditions can be forced on specific Ethereum transactions by monitoring the mempool.
For example, the classic \verb|ERC20 approve()| change can be front-run using this method.
Do not make assumptions about transaction order dependence.\\

\textbf{13. The security concern(s) in the given proxy-based implementation contract snippet is/are}\label{sec:exam4_q13}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;
import "https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/proxy/utils/Initializable.sol";

contract test is Initializable {

    // Assume other required functionality is correctly implemented

    address admin;
    uint256 rewards = 10;

    modifier onlyAdmin {
        require(msg.sender == admin);
        _;
    }

    function initialize (address _admin) external {
        require(_admin != address(0));
        admin = _admin;
    }

    function setRewards(uint256 _rewards) external onlyAdmin {
        rewards = _rewards;
    }
}
\end{lstlisting}

\textbf{Solution: B \& C.}

There are no imported contracts that need to be made upgradable (by implementing \verb|Initializable|).\\

Contracts imported from proxy-based upgradeable contracts should also be upgradeable where such contracts have been modified to use initializers instead of constructors.\\

Proxy-based upgradeable contracts need to use public initializer functions instead of constructors that need to be explicitly called only once.
Preventing multiple invocations of such initializer functions (e.g. via \verb|initializer| modifier from \verb|OpenZeppelin|'s \verb|Initializable| library) is a must.\\

Initializing state-variables in proxy-based upgradeable contracts should be done in initializer functions and not as part of the state variable declarations in which case they wonâ€™t be set.\\

\pagebreak

\textbf{14. The security concern(s) in the given contract snippet is/are}\label{sec:exam4_q14}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/IERC20.sol";

contract test {

    // Assume other required functionality is correctly implemented

    address admin;
    address token;

    constructor(address _admin, address _token) {
        require(_admin != address(0));
        require(_token != address(0));
        admin = _admin;
        token = _token;
    }

    modifier onlyAdmin {
        require(msg.sender == admin);
        _;
    }

    function payRewards(address[] calldata recipients, uint256[] calldata amounts) external onlyAdmin {
        for (uint i; i < recipients.length; i++) {
            IERC20(token).transfer(recipients[i], amounts[i]);
        }
    }
}
\end{lstlisting}

\textbf{Solution: A, C \& D.}

There's no guarantee that the passed arrays are of same length, so if one would be longer than the other one it can cause an \verb|Out Of Bounds| error, which is why D is correct.\\

Calls to external contracts inside a loop are dangerous (especially if the loop index can be user-controlled) because it could lead to DoS if one of the calls reverts or execution runs out of gas.
Avoid calls within loops, check that loop index cannot be user-controlled or is bounded.\\

\verb|ERC20 transfer()| does not return boolean, so contracts compiled with\linebreak\verb|solc >= 0.4.22| interacting with such functions will revert.
Use \verb|OpenZeppelin|'s \verb|SafeERC20| wrappers.\\

This is \verb|ERC20| token transfer and not Ether transfer (which throws on failure).
\verb|ERC20| transfer is typically expected to return a boolean but non-\verb|ERC20|-conforming tokens may return nothing or even revert which is typically why \verb|SafeERC20| is recommended.

\textbf{15. The vulnerability/vulnerabilities present in the given contract snippet is/are}\label{sec:exam4_q15}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;
import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/security/ReentrancyGuard.sol";

contract test {

 // Assume other required functionality is correctly implemented
 // For e.g. users have deposited balances in the contract

    mapping (address => uint256) balances;

    function withdrawBalance() external {
        msg.sender.call{value: balances[msg.sender]}("");
        balances[msg.sender] = 0;
    }
}
\end{lstlisting}

\textbf{Solution: A.}

Untrusted external contract calls could callback leading to unexpected results such as multiple withdrawals or out-of-order events.
Use check-effects-interactions pattern or reentrancy guards.\\

Not using \verb|OpenZeppelin|'s \verb|SafeMath| (or similar libraries) that check for overflows/underflows may lead to vulnerabilities or unexpected behavior if user/attacker can control the integer operands of such arithmetic operations.
\verb|Solc v0.8.0| introduced default overflow/underflow checks for all arithmetic operations.\\

\textbf{16. The security concern(s) in the given contract snippet is/are}\label{sec:exam4_q16}\\

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.4;

contract test {
// Assume other required functionality is correctly implemented
// Assume that hash is the hash of a message computed elsewhere in contract
// Assume that the contract does not make use of chainID or nonces in its logic

function verify(address signer, bytes32 memory hash, bytes32 sigR, bytes32 sigS, uint8 sigV) internal view returns (bool) {
   return signer == ecrecover(hash, sigV, sigR, sigS);
}
\end{lstlisting}

\textbf{Solution: A, B, C \& D.}

The \verb|ecrecover| function is susceptible to signature malleability which could lead to replay attacks.
Consider using \verb|OpenZeppelin|'s \verb|ECDSA| library.\\


The vulnerability occurs when a digital signature is valid for multiple transactions, which can happen when one sender (say Alice) sends money to multiple recipients through a proxy contract (instead of initiating multiple transactions).
In the proxy contract mechanism, Alice can send a digitally signed message off-chain (e.g., via email) to the recipients, similar to writing personal checks in the real world, to let the recipients withdraw money from the proxy contract via transactions.
To assure that Alice does approve a certain payment, the proxy contract verifies the validity of the digital signature in question.
However, if the signature does not give the due information (e.g., \verb|nonce|, proxy contract address), then a malicious recipient can replay the message multiple times to withdraw extra payments.
This vulnerability was first exploited in a replay attack against smart contracts.
This vulnerability can be prevented by incorporating the due information in each message, such as a \verb|nonce| value and timestamps.\\

Indistinguishable Chains: This vulnerability was first observed from the cross-chain replay attack when Ethereum was divided into two chains, namely, ETH and ETC.
Recall that Ethereum uses \verb|ECDSA| to sign transactions.
Prior to the hard fork for EIP-155, each transaction consisted of six fields (i.e., \verb|nonce|, \verb|recipient|, \verb|value|, \verb|input|, \verb|gasPrice| and \verb|gasLimit|).
However, the digital signatures were not chain-specific, because no chain-specific information was even known back then.
As a consequence, a transaction created for one chain can be reused for another chain.
This vulnerability has been eliminated by incorporating \verb|chainID| into the fields.\\

\verb|ecrecover() returns (address)| recovers the address associated with the public key from elliptic curve signature or return zero on error.\\

