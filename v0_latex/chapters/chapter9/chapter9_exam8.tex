\section{Audit Findings 201 Quiz}

All questions in this quiz are based on the \verb|InSecureumNFT| contract snippet.
\verb|InSecureumNFT| is a \verb|NFT| project that aims to distribute \verb|CrytptoSAFU NFT|s to its community where most of them are fairdropped based on past contributions and a few are sold.
\verb|CryptoSAFU|s with lower \verb|ID|s have more unqiue traits, may be valued higher and therefore require a random distribution for fairness.
Assume thar all strictly required \verb|ERC721| functionaity (not shown) and any other required functionality (not shown) are implemented correctly.
Only functionality specific to the sale and minting of NFTs is shown in this contract snippet.

\begin{lstlisting}[language=Solidity, style=solStyle]
pragma solidity 0.8.0;

interface ERC721TokenReceiver{function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes calldata _data) external returns(bytes4);}

// Assume that all strictly required ERC721 functionality (not shown) is implemented correctly
// Assume that any other required functionality (not shown) is implemented correctly
contract InSecureumNFT {
    bytes4 internal constant MAGIC_ERC721_RECEIVED = 0x150b7a02;
    uint public constant TOKEN_LIMIT = 10; // 10 for testing, 13337 for production
    uint public constant SALE_LIMIT = 5; // 5 for testing, 1337 for production

    mapping (uint256 => address) internal idToOwner;
    uint internal numTokens = 0;
    uint internal numSales = 0;
    address payable internal deployer;
    address payable internal beneficiary;
    bool public publicSale = false;
    uint private price;
    uint public saleStartTime;
    uint public constant saleDuration = 13*13337; // 13337 blocks assuming 13s block times 
    uint internal nonce = 0;
    uint[TOKEN_LIMIT] internal indices;
 
    constructor(address payable _beneficiary) {
        deployer = payable(msg.sender);
        beneficiary = _beneficiary;
    }

    function startSale(uint _price) external {
        require(msg.sender == deployer || _price != 0, "Only deployer and price cannot be zero");
        price = _price;
        saleStartTime = block.timestamp;
        publicSale = true;
    }

    function isContract(address _addr) internal view returns (bool addressCheck) {
        uint256 size;
        assembly { size := extcodesize(_addr) }
        addressCheck = size > 0;
    }

    function randomIndex() internal returns (uint) {
        uint totalSize = TOKEN_LIMIT - numTokens;
        uint index = uint(keccak256(abi.encodePacked(nonce, msg.sender, block.difficulty, block.timestamp))) % totalSize;
        uint value = 0;
        if (indices[index] != 0) {
            value = indices[index];
        } else {
            value = index;
        }
        if (indices[totalSize - 1] == 0) {
            indices[index] = totalSize - 1;
        } else {
            indices[index] = indices[totalSize - 1];
        }
        nonce += 1;
        return (value + 1);
    }

    // Calculate the mint price
    function getPrice() public view returns (uint) {
        require(publicSale, "Sale not started.");
        uint elapsed = block.timestamp - saleStartTime;
        if (elapsed > saleDuration) {
            return 0;
        } else {
            return ((saleDuration - elapsed) * price) / saleDuration;
        }
    }
    
    // SALE_LIMIT is 1337 
    // Rest i.e. (TOKEN_LIMIT - SALE_LIMIT) are reserved for community distribution (not shown)
    function mint() external payable returns (uint) {
        require(publicSale, "Sale not started.");
        require(numSales < SALE_LIMIT, "Sale limit reached.");
        numSales++;
        uint salePrice = getPrice();
        require((address(this)).balance >= salePrice, "Insufficient funds to purchase.");
        if ((address(this)).balance >= salePrice) {
            payable(msg.sender).transfer((address(this)).balance - salePrice);
        }
        return _mint(msg.sender);
    }

    // TOKEN_LIMIT is 13337
    function _mint(address _to) internal returns (uint) {
        require(numTokens < TOKEN_LIMIT, "Token limit reached.");
        // Lower indexed/numbered NFTs have rare traits and may be considered
        // as more valuable by buyers => Therefore randomize
        uint id = randomIndex();
        if (isContract(_to)) {
            bytes4 retval = ERC721TokenReceiver(_to).onERC721Received(msg.sender, address(0), id, "");
            require(retval == MAGIC_ERC721_RECEIVED);
        }
        require(idToOwner[id] == address(0), "Cannot add, already owned.");
        idToOwner[id] = _to;
        numTokens = numTokens + 1;
        beneficiary.transfer((address(this)).balance);
        return id;
    }
}
\end{lstlisting}

\textbf{1. Missing zero-address check(s) in the contract}

\begin{enumerate}[label=\Alph*.]
\item May allow anyone to start the sale.
\item May put the NFT sale proceeds at risk.
\item May burn the newly minted NFTs.
\item None of the above.
\end{enumerate}

Find the solution \hyperref[sec:exam8_q1]{here}.\\

\textbf{2. Given that lower indexed/numbered \texttt{CryptoSAFU NFT}s have rarer traits (and are considered more valuable as commented in \texttt{\_mint}), the implementation of \texttt{InSecureumNFT} is susceptible to the following exploits}

\begin{enumerate}[label=\Alph*.]
    \item Buyers can repeatedly mint and revert until they receive desired NFT.
    \item Buyers can generate addresses to mint until they receive desired NFT.
    \item Miners can manipulate \verb|block.timestamp| to facilitate minting of desired NFT.
    \item None of the above.
\end{enumerate}

Find the solution \hyperref[sec:exam8_q2]{here}.\\

\pagebreak

\textbf{3. The \texttt{getPrice()} function}

\begin{enumerate}[label=\Alph*.]
    \item Is expected to reduce the mint price over time after sale starts.
    \item Allows free mints after \~ 13337 blocks from when \verb|startSale()| is called.
    \item Visibility should be changed to \verb|external|.
    \item None of the above.
\end{enumerate}

Find the solution \hyperref[sec:exam8_q3]{here}.\\

\textbf{4. \texttt{InSecureumNFT} contract is}

\begin{enumerate}[label=\Alph*.]
    \item Not susceptible to reentrancy given the absence of external contract calls.
    \item Not susceptible to integer overflow/wrapping given the compiler version used and the absence of \verb|unchecked| blocks.
    \item Susceptible to reentrancy during minting.
    \item Perfectly safe for production.
\end{enumerate}

Find the solution \hyperref[sec:exam8_q4]{here}.\\

\textbf{5. Assuming \texttt{InSecureumNFT} contract is deployed in production (i.e. live for users) on mainnet without any changes to shown code}

\begin{enumerate}[label=\Alph*.]
    \item Use of evident test configuration wil cause fewer NFTs to be minted than expected in production.
    \item Illustrates the lack of best-practice for test parametrization to be removed or kept separate from production code.
    \item It will behave as documented in code to mint the expected number of NFTs in production.
    \item None of the above.
\end{enumerate}

Find the solution \hyperref[sec:exam8_q5]{here}.\\

\textbf{6. The function \texttt{startSale()}}

\begin{enumerate}[label=\Alph*.]
    \item May be succesfully called/executed by anyone.
    \item May be succesfully called/executed with \verb|_price| of 0.
    \item Must be called for minting to happen succesfully.
    \item None of the above.
\end{enumerate}

Find the solution \hyperref[sec:exam8_q6]{here}.\\

\textbf{7. The minting of NFTs}

\begin{enumerate}[label=\Alph*.]
    \item Requires an exact amount of ETH to be paid by the buyer.
    \item Refunds excess ETH paid by buyer back to the buyer.
    \item Transfers the NFT \verb|salePrice| to the \verb|beneficiary| address.
    \item May be optimized to prevent any zero ETH transfers in its refund mechanism.
\end{enumerate}

Find the solution \hyperref[sec:exam8_q7]{here}.\\

\textbf{8. The NFT sale}

\begin{enumerate}[label=\Alph*.]
    \item May be restarted by anyone any number of times.
    \item Can be started exactly once by \verb|deployer|.
    \item Is missing an additional check on \verb|publicSale|.
    \item Is missing an event emit in \verb|startSale|.
\end{enumerate}

Find the solution \hyperref[sec:exam8_q8]{here}.\\